ALTER PROCEDURE FGEN_SP_NOTIFICACION_RENOVACION
/****************************************************************************************/  
/* Procedimiento: FGEN_SP_NOTIFICACION_RENOVACION						       			*/
/* Descripcion  : Obtiene los colaboradores cuyo contrato esta por vencer 
				  o esta vencido														*/ 
/* Encargado    : Luis Bustamante	  													*/  
/* Fecha y hora : 20100127																*/ 
/* Fecha y hora : 23/02/2012															*/
/* Version		: 1.0.5																	*/
/****************************************************************************************/
	@CH_CODIGO_COMPANIA		CHAR(3),
	@VC_ESTADO_NOTIFICACION	VARCHAR(8),
	@VC_TIPO_PROGRAMACION	VARCHAR(7)
AS
BEGIN
	SET NOCOUNT ON
	--DECLARACION VARIABLES
	DECLARE @IN_CODIGO_SEDE								INT,
			@IN_NUMERO_MENSAJE							INT,
			@IN_DIAS_VENCER_7							INT,
			@IN_DIAS_VENCER_30							INT,
			@IN_DIAS_VENCER_60							INT,
			@IN_DIAS_VENCER_90							INT,
			@IN_DIAS_VENCER_MAX							INT,
			@IN_CANTIDAD_RENOVAR						INT,
			@IN_CODIGO_RESPONSABLE_UNIDAD				INT,
			@CH_ROL_CENTRALIZADO						CHAR(3),
			@CH_ROL_DESCENTRALIZADO						CHAR(3),
			@CH_CODIGO_HERRAMIENTA						CHAR(3),
			@VC_SEMANAL									VARCHAR(8),
			@VC_MENSUAL									VARCHAR(8),
			@VC_NOMBRE_AREA								VARCHAR(60),
			@VC_CORREO_JEFE_UNIDAD						VARCHAR(200),
			@VC_CORREO_ADM_DESCENTRALIZADO				VARCHAR(MAX),
			@VC_CORREO_ADM_RRHH							VARCHAR(MAX),
			@IN_CODIGO_AREA								INT
			
		-- SETEAR VALORES
		SELECT	@IN_NUMERO_MENSAJE			=	1,
				@CH_ROL_CENTRALIZADO		=	'003',
				@CH_ROL_DESCENTRALIZADO		=	'013',
				@CH_CODIGO_HERRAMIENTA		=	'03'
				
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R04',@VC_SEMANAL OUTPUT
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R05',@VC_MENSUAL OUTPUT
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R06',@IN_DIAS_VENCER_7 OUTPUT
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R07',@IN_DIAS_VENCER_30 OUTPUT
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R08',@IN_DIAS_VENCER_60 OUTPUT
		EXEC FGEN_SP_OBTENER_VALOR_REF_GENERAL	'089',@CH_CODIGO_COMPANIA,'R09',@IN_DIAS_VENCER_90 OUTPUT
		
		--Obtener Dias Maximos a vencerse 	
		SELECT @IN_DIAS_VENCER_MAX = CASE @VC_TIPO_PROGRAMACION 
										WHEN @VC_SEMANAL THEN @IN_DIAS_VENCER_7
										WHEN @VC_MENSUAL THEN @IN_DIAS_VENCER_90
										ELSE 0
									END 
		--Creamos tabla temporal de Administradores descentralizados
		CREATE TABLE #ADMINISTRADORES_DESCENTRALIZADO
		(
			IN_CODIGO_SEDE		INT,
			VC_CORREO			VARCHAR(256)	collate database_default
		)
				
		--insertamos los administradores descentralizados
		INSERT INTO #ADMINISTRADORES_DESCENTRALIZADO
		(
			IN_CODIGO_SEDE	 ,
			VC_CORREO			
		)
		SELECT	DISTINCT	IN_CODIGO_SEDE		= URH.IN_CODIGO_SEDE,
							VC_CORREO			= U.VC_CORREO
		FROM	FGEN_TA_USUARIO_ROL_HERRAMIENTA URH (NOLOCK)
		INNER JOIN FGEN_TA_USUARIO	U (NOLOCK)
		ON		URH.IN_CODIGO_USUARIO		= U.IN_CODIGO_USUARIO
		WHERE	URH.CH_CODIGO_HERRAMIENTA	= @CH_CODIGO_HERRAMIENTA
			AND	URH.CH_CODIGO_ROL			= @CH_ROL_DESCENTRALIZADO
			AND	URH.CH_INDICADOR_RESPONSABLE	= '1'
			AND	URH.CH_SITUACION_REGISTRO		= 'A'
			AND	U.CH_ESTADO						= 'A'
			AND	U.CH_SITUACION_REGISTRO			= 'A'
		--Creamos tabla temporal de Administradores Centralizados	
		CREATE TABLE #ADMINISTRADORES_RRHH
		(
			VC_CORREO			VARCHAR(256)	collate database_default
		)
		--insertamos los administradores descentralizados
		INSERT INTO #ADMINISTRADORES_RRHH
		(
			VC_CORREO			
		)
		SELECT	DISTINCT VC_CORREO			= U.VC_CORREO
		FROM	FGEN_TA_USUARIO_ROL_HERRAMIENTA URH (NOLOCK)
		INNER JOIN FGEN_TA_USUARIO	U (NOLOCK)
		ON		URH.IN_CODIGO_USUARIO		= U.IN_CODIGO_USUARIO
		WHERE	URH.CH_CODIGO_HERRAMIENTA	= @CH_CODIGO_HERRAMIENTA
			AND	URH.CH_CODIGO_ROL			= @CH_ROL_CENTRALIZADO
			AND	URH.CH_INDICADOR_RESPONSABLE	= '1'
			AND	URH.CH_SITUACION_REGISTRO		= 'A'
			AND	U.CH_ESTADO						= 'A'
			AND	U.CH_SITUACION_REGISTRO			= 'A'
		--Creamos Tabla Temporal
		CREATE TABLE #TEMPORAL_EMPLEADOS_NOTIFICAR_CONTRATO_A_VENCER(
			IN_CODIGO_EMPLEADO							INT,
			VC_CUC										VARCHAR(10)		collate database_default,
			VC_COLABORADOR								VARCHAR(60)		collate database_default,
			DT_FECHA_VENCIMIENTO						DATETIME,
			IN_CODIGO_AREA								INT,
			VC_NOMBRE_AREA								VARCHAR(128)	collate database_default,
			IN_CODIGO_SEDE								INT,
			VC_NOMBRE_SEDE								VARCHAR(128)	collate database_default,
			IN_CODIGO_PUESTO							INT,
			VC_NOMBRE_PUESTO							VARCHAR(128)	collate database_default,
			NRO_DIAS_VENCIMIENTO_CONTRATO				INT,
			VC_ESTADO									VARCHAR(100)	collate database_default,
			VC_CORREO_JEFE_UNIDAD						VARCHAR(256)	collate database_default,
			IN_CODIGO_RESPONSABLE_UNIDAD				INT,
			VC_NOMBRE_RESPONSABLE_UNIDAD				VARCHAR(70)		collate database_default		
		)
		--Insertamos en la tabla temporal
		INSERT INTO #TEMPORAL_EMPLEADOS_NOTIFICAR_CONTRATO_A_VENCER 
		(
			IN_CODIGO_EMPLEADO,
			VC_CUC,
			VC_COLABORADOR,
			DT_FECHA_VENCIMIENTO,
			IN_CODIGO_AREA,
			VC_NOMBRE_AREA,
			IN_CODIGO_SEDE,
			VC_NOMBRE_SEDE,
			IN_CODIGO_PUESTO,
			VC_NOMBRE_PUESTO,
			NRO_DIAS_VENCIMIENTO_CONTRATO,
			VC_ESTADO,
			VC_CORREO_JEFE_UNIDAD,
			IN_CODIGO_RESPONSABLE_UNIDAD,
			VC_NOMBRE_RESPONSABLE_UNIDAD
		)
		
		SELECT DISTINCT
				IN_CODIGO_EMPLEADO						= E.IN_CODIGO_EMPLEADO,
				VC_CUC									= ISNULL(E.VC_IDENTIFICADOR,''),
				VC_COLABORADOR							= E.VC_NOMBRE_COMPLETO,
				DT_FECHA_VENCIMIENTO					= R.DT_FECHA_TERMINO_CONTRATO,
				IN_CODIGO_AREA							= E.IN_CODIGO_AREA,
				VC_NOMBRE_AREA							= (SELECT dbo.FORG_FN_OBTENER_NOMBRE_AREA(E.IN_CODIGO_AREA,'es')),
				IN_CODIGO_SEDE							= E.IN_CODIGO_SEDE,
				VC_NOMBRE_SEDE							= (SELECT dbo.FORG_FN_OBTENER_NOMBRE_SEDE(E.IN_CODIGO_SEDE,'es')),
				IN_CODIGO_PUESTO						= E.IN_CODIGO_PUESTO,
				VC_NOMBRE_PUESTO						= (SELECT dbo.FORG_FN_OBTENER_NOMBRE_PUESTO(E.IN_CODIGO_PUESTO,'es')),
				NRO_DIAS_VENCIMIENTO_CONTRATO			= DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO),
				VC_ESTADO								= CASE	
																WHEN	DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) BETWEEN 1							AND	@IN_DIAS_VENCER_7	THEN	'POR VENCER 7 DIAS' --REPLACE('POR VENCER # DIAS','#',@IN_DIAS_VENCER_MAX)
																WHEN	DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) BETWEEN (@IN_DIAS_VENCER_7+1)		AND	@IN_DIAS_VENCER_30	THEN	'POR VENCER 30 DIAS'--REPLACE('POR VENCER # DIAS','#',@IN_DIAS_VENCER_MAX)
																WHEN	DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) BETWEEN (@IN_DIAS_VENCER_30+1)	AND	@IN_DIAS_VENCER_60	THEN	'POR VENCER 60 DIAS'--REPLACE('POR VENCER # DIAS','#',@IN_DIAS_VENCER_MAX)
																WHEN	DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) BETWEEN (@IN_DIAS_VENCER_60+1)	AND	@IN_DIAS_VENCER_90	THEN	'POR VENCER 90 DIAS'--REPLACE('POR VENCER # DIAS','#',@IN_DIAS_VENCER_MAX)
																WHEN	DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) <= 0														THEN	'VENCIDOS'
															END,
				VC_CORREO_JEFE_UNIDAD					= (SELECT VC_CORREO FROM dbo.FPER_FN_OBTIENE_AREA_SEDE_JEFE_INMEDIATO(E.IN_CODIGO_EMPLEADO)),
				IN_CODIGO_RESPONSABLE_UNIDAD			= (SELECT IN_CODIGO_RESPONSABLE FROM dbo.FPER_FN_OBTIENE_AREA_SEDE_JEFE_INMEDIATO(E.IN_CODIGO_EMPLEADO)),
				VC_NOMBRE_RESPONSABLE_UNIDAD			= (SELECT VC_NOMBRE_RESPONSALBE FROM dbo.FPER_FN_OBTIENE_AREA_SEDE_JEFE_INMEDIATO(E.IN_CODIGO_EMPLEADO))
		FROM	FPER_TA_SEGUIMIENTO_RENOVACION R (NOLOCK)
		INNER JOIN FPER_TA_EMPLEADO E  (NOLOCK)
			ON	R.IN_CODIGO_EMPLEADO	= E.IN_CODIGO_EMPLEADO
			AND	E.CH_SITUACION_REGISTRO	= 'A'
			AND	E.CH_CODIGO_COMPANIA	= @CH_CODIGO_COMPANIA
			AND	R.CH_SITUACION_REGISTRO = 'A'
			--AZ solo se envia del personal activo.
			AND	E.CH_CODIGO_ESTADO_EMPLEADO IN ('AC','LS','LC')
		WHERE	(DATEDIFF(d,GETDATE(),R.DT_FECHA_TERMINO_CONTRATO) BETWEEN 0 AND @IN_DIAS_VENCER_MAX
					OR
				DATEDIFF(d,R.DT_FECHA_TERMINO_CONTRATO,GETDATE())>0)
		AND     E.CH_REGISTRA_ASISTENCIA NOT IN ('06')
		ORDER	BY R.DT_FECHA_TERMINO_CONTRATO ASC, E.VC_NOMBRE_COMPLETO ASC
	--GENERAMOS LOS CORREOS
	--Construimos la cabecera de tabla resumen
	DECLARE @NVCABECERARESUMEN NVARCHAR(MAX)
	SET @NVCABECERARESUMEN =			
			N'<table border="1" cellspacing="0" cellpadding="0" width="50%">' +
			N'<tr align="center">' +
			N'<th  style="width:70%">CONTRATO</th>' +
			N'<th  style="width:30%">CANTIDAD</th>' +
			N'</tr>' 
	--Construimos la cabecera del excel
	DECLARE @NVCABECERAEXCEL NVARCHAR(MAX)
	SET @NVCABECERAEXCEL =
			N'<table border="1">' +
			N'<tr>' + 
			N'<th>CUC</th>' +
			N'<th>COLABORADOR</th>' + 
			N'<th>PUESTO</th>' +
			N'<th>AREA</th>' + 
			N'<th>SEDE</th>' + 
			N'<th>JEFE RESPONSABLE</th>' +
			N'<th>FECHA DE VENCIMIENTO</th>' + 
			N'<th>ESTADO</th>' + 
			N'</tr>' 
				
	--EXTRAEMOS LOS EMPLEADOS NOTIFICAR AL JEFE DE LA UNIDAD
	DECLARE	CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD CURSOR FOR
	(
		SELECT	DISTINCT	IN_CODIGO_RESPONSABLE_UNIDAD,
							VC_CORREO_JEFE_UNIDAD,
							VC_NOMBRE_AREA,
							IN_CODIGO_AREA
		FROM	#TEMPORAL_EMPLEADOS_NOTIFICAR_CONTRATO_A_VENCER (NOLOCK)
		WHERE	LTRIM(RTRIM(VC_CORREO_JEFE_UNIDAD))<>''
	)
	OPEN	CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD          
	FETCH	NEXT FROM CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD            
	INTO	@IN_CODIGO_RESPONSABLE_UNIDAD,
			@VC_CORREO_JEFE_UNIDAD,
			@VC_NOMBRE_AREA,
			@IN_CODIGO_AREA
	WHILE	@@FETCH_STATUS = 0                    
	BEGIN   
		
		EXECUTE FGEN_SP_GENERACION_RESUMEN_RENOVACIONES @CH_CODIGO_COMPANIA,
														@IN_CODIGO_RESPONSABLE_UNIDAD,
														0,
														@VC_NOMBRE_AREA,
														@VC_TIPO_PROGRAMACION,
														@VC_CORREO_JEFE_UNIDAD,
														@NVCABECERARESUMEN,
														@NVCABECERAEXCEL,
														@IN_NUMERO_MENSAJE,
														@IN_CODIGO_AREA 
		 
		SET	@IN_NUMERO_MENSAJE	=	@IN_NUMERO_MENSAJE	+	1
		FETCH NEXT FROM CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD            
		INTO	@IN_CODIGO_RESPONSABLE_UNIDAD,
				@VC_CORREO_JEFE_UNIDAD,
				@VC_NOMBRE_AREA,
				@IN_CODIGO_AREA
	END          
	CLOSE	CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD          
	DEALLOCATE	CUROBTENEREMPLEADOSNOTIFICARJEFEUNIDAD  
	
	--EXTRAEMOS LOS EMPLEADOS NOTIFICAR AL ADMINISTRADOR DESCENTRALIZADO
	DECLARE	CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO CURSOR FOR
	(
		SELECT	DISTINCT AD.IN_CODIGO_SEDE
		FROM #ADMINISTRADORES_DESCENTRALIZADO AD
		INNER JOIN #TEMPORAL_EMPLEADOS_NOTIFICAR_CONTRATO_A_VENCER TE
		ON AD.IN_CODIGO_SEDE = TE.IN_CODIGO_SEDE
		WHERE AD.VC_CORREO IS NOT NULL	AND	LEN(RTRIM(AD.VC_CORREO))>0
	)
	OPEN	CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO          
	FETCH	NEXT FROM CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO            
	INTO	@IN_CODIGO_SEDE 
	WHILE	@@FETCH_STATUS = 0                    
	BEGIN      
		--Obtenemos los correos de acuerdo a la Sede
		SET @VC_CORREO_ADM_DESCENTRALIZADO = ''
		SELECT @VC_CORREO_ADM_DESCENTRALIZADO = @VC_CORREO_ADM_DESCENTRALIZADO + ';' + VC_CORREO FROM #ADMINISTRADORES_DESCENTRALIZADO
		IF LEFT(@VC_CORREO_ADM_DESCENTRALIZADO,1) = ';' SET @VC_CORREO_ADM_DESCENTRALIZADO = SUBSTRING(@VC_CORREO_ADM_DESCENTRALIZADO,2,LEN(@VC_CORREO_ADM_DESCENTRALIZADO))
		EXECUTE FGEN_SP_GENERACION_RESUMEN_RENOVACIONES @CH_CODIGO_COMPANIA,
														0,
														@IN_CODIGO_SEDE,
														'ADM DESCENTRALIZADO',
														@VC_TIPO_PROGRAMACION,
														@VC_CORREO_ADM_DESCENTRALIZADO,
														@NVCABECERARESUMEN,
														@NVCABECERAEXCEL,
														@IN_NUMERO_MENSAJE,
														0  
	
		SET	@IN_NUMERO_MENSAJE	=	@IN_NUMERO_MENSAJE	+	1
		
		FETCH NEXT FROM CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO            
		INTO @IN_CODIGO_SEDE
	END       
	CLOSE	CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO          
	DEALLOCATE	CUROBTENEREMPLEADOSNOTIFICARADMINISTRADORDESCENTRALIZADO  
	
	--EXTRAEMOS LOS EMPLEADOS NOTIFICAR AL ADMINISTRADOR CENTRALIZADO
	--Obtenemos los correos de los administradores	
	SET @VC_CORREO_ADM_RRHH = ''
	SELECT @VC_CORREO_ADM_RRHH = @VC_CORREO_ADM_RRHH + ';' + VC_CORREO FROM #ADMINISTRADORES_RRHH
	
	IF LEFT(@VC_CORREO_ADM_RRHH,1) = ';' SET @VC_CORREO_ADM_RRHH = SUBSTRING(@VC_CORREO_ADM_RRHH,2,LEN(@VC_CORREO_ADM_RRHH))

	IF @VC_CORREO_ADM_RRHH <> '' AND @VC_CORREO_ADM_RRHH IS NOT NULL
	BEGIN

		EXECUTE FGEN_SP_GENERACION_RESUMEN_RENOVACIONES @CH_CODIGO_COMPANIA,
														0,
														0,
														'ADM RRHH',
														@VC_TIPO_PROGRAMACION,
														@VC_CORREO_ADM_RRHH,
														@NVCABECERARESUMEN,
														@NVCABECERAEXCEL,
														@IN_NUMERO_MENSAJE,
														0

	END

	DROP TABLE #ADMINISTRADORES_RRHH
	DROP TABLE #ADMINISTRADORES_DESCENTRALIZADO
	DROP TABLE #TEMPORAL_EMPLEADOS_NOTIFICAR_CONTRATO_A_VENCER
	
	
END
